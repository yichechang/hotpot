from pathlib import Path

configfile: workflow.source_path('./config.yaml')

wildcard_constraints:
    sample = "[\w-]+",

def get_all_sample_something(something):
    samples = config["samples"]
    return [s[something] for s in samples]

SAMPLE_NAMES = get_all_sample_something("name")


rule all:
    input:
        [
            "results/output/fragment_length_distri.pdf",
            "results/output/fragment_length_stats.csv",
        ]

rule all_envs:
    """
    Run this rule with internect access. This is a workaround for creating
    conda environments associated with rules that are not discoverable 
    until runtime (e.g., rules downstream to checkpoint rules).
    """
    conda:
        "envs/{env}.yaml"
    output:
        touch(".envs/{env}.created")

def get_bam_filenames(wildcards):
    return Path(config['bam_dir']) / config['bam_filename_pattern']

rule sort_bam_by_name:
    input: 
        get_bam_filenames
    output:
        "results/bam_sorted_by_name/{sample}.bam"
    conda:
        "envs/samtools.yaml"
    shell:
        """
        samtools sort -n -o {output} {input:q}
        """

rule count_fragment_lengths:
    input:
        "results/bam_sorted_by_name/{sample}.bam"
    output:
        "results/fragment_lengths/{sample}.csv"
    conda:
        "envs/pysam_pandas.yaml"
    script:
        "scripts/count_fragment_lengths.py"

rule plot_fragment_lengths:
    input:
        csvs=expand("results/fragment_lengths/{sample}.csv",
                    sample=SAMPLE_NAMES),
    output:
        fig="results/output/fragment_length_distri.pdf",
        stats="results/output/fragment_length_stats.csv",
    params:
        samples=get_all_sample_something("desc"),
        binwidth=config['binwidth'],
        binrange=tuple(config['binrange']),
        stat=config['stat'],
        tick=config['tick'],
        gatings=config['gatings'],
    conda:
         "envs/plot.yaml"
    script:
        "scripts/plot_distribution.py"
